
class_name Entity 
extends CharacterBody2D 

@export_group("Nodes")
@export var sprite: AnimatedSprite2D

@export_group("Title")
@export var type_name: String = "none"
@export var group: String = "none"

@export_group("Stats")
@export var lifetime: int = 3600
@export var max_health: int = 100
var income_damage: int = 0

@export var outcome_damage: int = 5
@export var max_hunger: int = 100
var hunger: int = 0

@export var faction: String = "none"
@export var speed: int = 10

@onready var sight_area: Area2D = $Sight_Area
@onready var collision: CollisionShape2D = $CollisionShape2D

var is_ai_enabled: bool = true
var schedule: Array = []
var current_task: Dictionary = {}

var subtasks: Array = []
var current_subtask: Dictionary = {}
var stuck_timer: float = 0.0
var STUCK_THRESHOLD: float = 3.0
var last_position: Vector2

var environment: World
var specific_commands: Array = [
	"step", "mv", "mv_s", "pos", "feed", "expire", "lifetime", "kill", "play", "stop", "stopall", "st", "team", "ai"
]
var prefered_pos: Vector2
var face_dir: Vector2i
enum direction {UP, DOWN, LEFT, RIGHT}

func _ready() -> void:
	sprite = $Sprite
	face_dir = Vector2i(0, 1)
	idle()
	prefered_pos = global_position
	if environment:
		create_tween().tween_property(self, "scale", Vector2(1.0, 1.0), 1.0).set_trans(Tween.TRANS_SINE)

func _input_event(_viewport, event, _shape_idx):
	if event is InputEventMouseButton and \
	event.button_index == MOUSE_BUTTON_LEFT\
	and event.pressed:
		Global.emit_signal("entity_selected", self)

var timer: float = 0.0
func is_tick(delta: float) -> bool:
	timer += delta
	if timer >= 1.0:
		timer = 0.0
		lifetime -= 1
		if lifetime <= 0:
			on_lifetime_end()
		return true
	return false

func _physics_process(delta: float) -> void:
	var tick = is_tick(delta)
	
	if tick and hunger < max_hunger:
		hunger += 1
	if income_damage > max_health: 
		die()
		return
	if hunger >= max_hunger and tick: income_damage+=1
	elif income_damage > 0 and tick: 
		var heal_rate = clamp(10 - (hunger / 10), 0, 10)
		income_damage -= heal_rate
	if tick and is_ai_enabled and environment:
		AI(delta)
	
	if not current_subtask.is_empty():
		exec_subtask(current_subtask, delta)
	elif not subtasks.is_empty():
		current_subtask = subtasks.pop_front()
		exec_subtask(current_subtask, delta)
	elif not current_task.is_empty():
		exec_task(current_task, delta) 
	elif not schedule.is_empty():
		current_task = schedule.pop_front()
	else: idle()
	if tick:
		last_position = global_position

func add_task(type: String, args: Array) -> void:
	var task: Dictionary = {"type": type}
	match type:
		"mv": 
			var pos = Vector2(args[0], args[1])
			if not args[2]:
				prefered_pos = pos
			task["pos"] = pos
		"wait": task["time"] = args[0]
		
	schedule.append(task)

func add_subtask(type: String, args: Array) -> void:
	var subtask: Dictionary = {"type": type}
	match type:
		"mv":
			var pos = Vector2i(args[0], args[1])
			subtask["pos"] = pos
			subtask["evade"] = args[2]
	subtasks.append(subtask)

func exec_task(task: Dictionary, delta: float) -> int:
	match task["type"]:
		"wait":
			task["time"] -= delta
			if task["time"] <= 0: 
				complete_task()
				return 1
		"mv":
			var pos = task["pos"]
			var point = environment.get_cell(Vector2i(global_position))
			var end_point = environment.get_cell(pos)
			var path = find_path(point, end_point)
			
			if subtasks.size() == 0:
				for cell in path:
					var cell_pos = environment.nav_grid.get_point_position(cell)
					add_subtask("mv", [cell_pos.x, cell_pos.y, false])
			
			if not subtasks.is_empty(): return 0
			else: 
				complete_task() 
				return 1
	return 1

func exec_subtask(task: Dictionary, delta: float) -> int:
	var status: int
	match task["type"]:
		"mv":
			var results = move_at(task["pos"])
			status = results["status"]
			
			if status == -1:
				complete_subtask()
				on_stuck_handle(results["obstacle"])
			
	if status == 1: complete_subtask()
	return status

func find_path(point: Vector2i, end_point: Vector2i) -> Array[Vector2i]:
	return environment.nav_grid.get_id_path(point, end_point)

func on_stuck_handle(obstacle: Area2D) -> void:
	var evade_point = global_position+Vector2(face_dir)*100
	subtasks.clear()
	
	if obstacle:
		var shape: Rect2 = obstacle.find_child("CollisionShape2D").shape.get_rect()
		turn("left")
		if shape.size.x > shape.size.y:
			evade_point = global_position+Vector2(face_dir)*shape.size.x*1.5
		else:
			evade_point = global_position+Vector2(face_dir)*shape.size.y*1.5
	else:
		evade_point = global_position
		match get_face_dir():
			direction.UP   : evade_point.x -= get_size()
			direction.DOWN : evade_point.x += get_size()
			direction.LEFT : evade_point.y += get_size()
			direction.RIGHT: evade_point.y -= get_size()
			
	current_subtask = {"type": "mv", "pos": evade_point, "evade": true}
	var dir = evade_point.direction_to(prefered_pos)
	if abs(dir.y) > abs(dir.x):
		add_subtask("mv", [prefered_pos.x, evade_point.y, false])
		add_subtask("mv", [prefered_pos.x, prefered_pos.y, false])
	else:
		add_subtask("mv", [evade_point.x, prefered_pos.y, false])
		add_subtask("mv", [prefered_pos.x, prefered_pos.y, false])

func exec_command(type: String, args: Array):
	match type:	
		"expire":
			lifetime = 0
		"lifetime":
			lifetime = int(args[0])
		"feed":
			hunger = 0
		"pos":
			if args.size() == 0:
				return "pos: %d %d" % [position.x, position.y]
			else:
				var x = int(args[0])
				var y = int(args[1])
				prefered_pos = Vector2(x, y)
				return "pref pos: %d %d" % [x, y]
		"sector":
			var sec = environment.get_sector_key(global_position)
			return "sec: %d %d" % [sec.x, sec.y]
		"team":
			return "faction: %s" % faction
		"play":
			sprite.play(args[0].replace("_"," "))
		"st":
			return "(%d) task: %s\n(%d) subtask: %s" % [schedule.size(), current_task.get("type"), subtasks.size(), current_subtask.get("type")]
		"kill":
			income_damage += max_health*100
		"stop":
			complete_task()
			subtasks.clear()
			complete_subtask()
		"stopall":
			schedule.clear()
			subtasks.clear()
			complete_task()
			complete_subtask()
			prefered_pos = global_position
			sprite.play("Idle Down")
		"mv": 
			add_task("mv", [int(args[0]), int(args[1]), false])
		"mv_s":
			var sec_key = Vector2i(int(args[0]), int(args[1]))
			var sec = environment.sectors.get(sec_key)
			if sec:
				var sec_center = sec.get_center()
				add_task("mv", [sec_center.x, sec_center.y, false])
		"step":
			add_task("mv", [global_position.x+int(args[0]), global_position.y+int(args[1]), false])
		"ai":
			match args[0]:
				"on", "1", "enable", "true": is_ai_enabled = true
				"off", "0", "disable", "false": is_ai_enabled = false
				_: is_ai_enabled = false

func complete_task() -> void:
	current_task.clear()
	idle()

func complete_subtask() -> void:
	current_subtask.clear()
	if subtasks.is_empty(): complete_task()

func idle() -> void:
	if not sprite: return
	match get_face_dir():
			direction.DOWN: sprite.play("Idle Down")
			direction.UP: sprite.play("Idle Up")
			direction.RIGHT: sprite.play("Idle Right")
			direction.LEFT: sprite.play("Idle Left")

func walk(dir: Vector2) -> void:
	if abs(dir.x) > abs(dir.y):
		sprite.play("Walk Right" if dir.x > 0 else "Walk Left")
		face_dir = Vector2(1, 0) if dir.x > 0 else Vector2(-1, 0)
	else:
		sprite.play("Walk Down" if dir.y > 0 else "Walk Up")
		face_dir = Vector2(0, 1) if dir.y > 0 else Vector2(0, -1)
	sight_area.rotation = Vector2.DOWN.angle_to(Vector2(face_dir))
	velocity = dir*speed
	move_and_slide()

func move_at(pos: Vector2i) -> Dictionary:
	var dir = global_position.direction_to(pos)
	if global_position.distance_to(pos) <= 15:
		return {"status": 1}
	var obstacles = sight_area.get_overlapping_areas()
	if obstacles.size() > 1 and not current_subtask["evade"]: return {"status": -1, "obstacle": obstacles[0]}
	
	if global_position.distance_to(last_position) < 1 and velocity != Vector2.ZERO:
		stuck_timer += 1
		if stuck_timer >= STUCK_THRESHOLD:
			stuck_timer = 0.0
			return {"status": -1, "obstacle": null}
	else:
		stuck_timer = max(0.0, stuck_timer - 1)
	
	walk(dir)
	return {"status": 0}

func move_to(target: Node2D) -> bool:
	return false

func turn(side: String) -> void:
	match side:
		"left": 
			face_dir = Vector2i(face_dir.y, -face_dir.x)
		"right":
			face_dir = Vector2i(-face_dir.y, face_dir.x)
	sight_area.rotation = Vector2.DOWN.angle_to(Vector2(face_dir))
	idle()

func eat(target: Node2D) -> void:
	pass

func get_face_dir() -> int:
	match face_dir:
		Vector2i(0, -1): return direction.UP
		Vector2i(0, 1) : return direction.DOWN
		Vector2i(-1, 0): return direction.LEFT
		Vector2i(1, 0) : return direction.RIGHT
	return direction.DOWN

func get_size() -> float:
	var rect: Rect2 = collision.shape.get_rect()
	var width = rect.end.x
	var height = rect.end.y
	if width > height:
		return width
	else: return height

func on_lifetime_end() -> void:
	die()

func die() -> void:
	var corpse_scene: PackedScene
	var corpse: Corpse
	
	match get_face_dir():
		direction.DOWN : sprite.play("Death Down")
		direction.UP   : sprite.play("Death Up")
		direction.LEFT : sprite.play("Death Left")
		direction.RIGHT: sprite.play("Death Right")
	
	await get_tree().create_timer(0.5).timeout
	if group != "none" and type_name != "none":
		corpse_scene = load("res://entity/%s/corpses/%s.tscn" % [group, type_name])
		corpse = corpse_scene.instantiate()
		corpse.face_dir = get_face_dir()
		corpse.global_position = global_position
		add_sibling(corpse)
	
	queue_free()

func is_busy() -> bool:
	return not (subtasks.is_empty() and\
	 schedule.is_empty() and\
	 current_subtask.is_empty() and\
	 current_task.is_empty())

func wander() -> void:
	if not is_busy() and randf() >= 0.8:
		var pos = global_position+Vector2(randi_range(-500, 500), randi_range(-500, 500))
		var tile = environment.get_cell(pos)
		if not environment.nav_grid.is_point_solid(tile):
			prefered_pos = pos

func AI(delta: float) -> void:
	if global_position.distance_to(prefered_pos) > 30 and not is_busy():
		add_task("mv", [prefered_pos.x, prefered_pos.y, false])
	wander()
